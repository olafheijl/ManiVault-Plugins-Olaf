cmake_minimum_required(VERSION 3.22)

option(MV_UNITY_BUILD "Combine target source files into batches for faster compilation" OFF)
option(USE_FAISS "Enable Faiss library integration" OFF)  # Add this line

# -----------------------------------------------------------------------------
# DVRView Plugin
# -----------------------------------------------------------------------------
project("DVRViewPlugin")

# -----------------------------------------------------------------------------
# CMake Options
# -----------------------------------------------------------------------------
set(CMAKE_MODULE_PATH ${CMAKE_CURRENT_SOURCE_DIR}/cmake)
set(CMAKE_INCLUDE_CURRENT_DIR ON)
set(CMAKE_AUTOMOC ON)

if(MSVC)
    set(CMAKE_CXX_FLAGS "${CMAKE_CXX_FLAGS} /DWIN32 /EHsc /MP /permissive- /Zc:__cplusplus")
    set(CMAKE_EXE_LINKER_FLAGS "${CMAKE_EXE_LINKER_FLAGS} /NODEFAULTLIB:LIBCMT")
    set(CMAKE_CXX_FLAGS_DEBUG "${CMAKE_CXX_FLAGS_DEBUG} /MDd")
    set(CMAKE_CXX_FLAGS_RELEASE "${CMAKE_CXX_FLAGS_RELEASE} /MD")
endif(MSVC)

# -----------------------------------------------------------------------------
# Dependencies
# -----------------------------------------------------------------------------
find_package(Qt6 COMPONENTS Widgets WebEngineWidgets OpenGL OpenGLWidgets REQUIRED)
find_package(ManiVault COMPONENTS Core PointData ImageData VolumeData CONFIG QUIET)

# --- OpenMP Support ---
find_package(OpenMP REQUIRED)
if(OpenMP_CXX_FOUND)
    message(STATUS "Found OpenMP: ${OpenMP_CXX_FLAGS}")
else()
    message(WARNING "OpenMP not found.")
endif()

# -----------------------------------------------------------------------------
# Conda Faiss Integration
# -----------------------------------------------------------------------------
if(USE_FAISS)
    # Set the path to your Faiss library.
    set(FAISS_DIR "" CACHE PATH "Path to your folder containing the Faiss include and library folders")
    if(FAISS_DIR STREQUAL "")
        message(FATAL_ERROR "FAISS_DIR is not set. Please set it to your folder that contains the Faiss include and library folders.")
    endif()

    if(WIN32)
        set(FAISS_INCLUDE_DIR "${FAISS_DIR}/Library/include")
        set(FAISS_LIBRARY_DIR "${FAISS_DIR}/Library/lib")
    else()
        set(FAISS_INCLUDE_DIR "${FAISS_DIR}/include")
        set(FAISS_LIBRARY_DIR "${FAISS_DIR}/lib")
    endif()

    message(STATUS "Using Faiss include directory: ${FAISS_INCLUDE_DIR}")
    message(STATUS "Using Faiss library directory: ${FAISS_LIBRARY_DIR}")

    include_directories(${FAISS_INCLUDE_DIR})
    link_directories(${FAISS_LIBRARY_DIR})
endif()

# -----------------------------------------------------------------------------
# hnswlib Integration
# -----------------------------------------------------------------------------
# Set the path to your hnswlib installation directory containing the header files.
set(HNSWLIB_DIR "" CACHE PATH "Path to your hnswlib installation directory containing hnswlib headers")
if(HNSWLIB_DIR STREQUAL "")
    message(FATAL_ERROR "HNSWLIB_DIR is not set. Please set it to your hnswlib installation directory.")
endif()

message(STATUS "Using hnswlib directory: ${HNSWLIB_DIR}")

include_directories(${HNSWLIB_DIR})

# -----------------------------------------------------------------------------
# Source Files
# -----------------------------------------------------------------------------
# Define the plugin sources
set(PLUGIN_SOURCES
    src/DVRViewPlugin.h
    src/DVRViewPlugin.cpp
)
set(JSON
    src/DVRViewPlugin.json
)
set(PLUGIN_RENDERERS
    src/VolumeRenderer.h
    src/VolumeRenderer.cpp
    src/MCArrays.h
)
set(PLUGIN_GRAPHICS
    src/TrackballCamera.h 
    src/TrackballCamera.cpp
)
set(PLUGIN_WIDGETS
    src/DVRWidget.h
    src/DVRWidget.cpp
)
set(PLUGIN_ACTIONS
    src/SettingsAction.h
    src/SettingsAction.cpp
    src/GlobalSettingsAction.h
    src/GlobalSettingsAction.cpp
)
set(RESOURCES
    res/ViewerResources.qrc
)

qt_add_resources(RESOURCE_FILES ${RESOURCES})

set(SOURCES ${PLUGIN_SOURCES} ${PLUGIN_WIDGETS} ${PLUGIN_GRAPHICS} ${PLUGIN_RENDERERS} ${PLUGIN_ACTIONS})

source_group( Plugin FILES ${PLUGIN_SOURCES})
source_group( Widget FILES ${PLUGIN_WIDGETS})
source_group( Actions FILES ${PLUGIN_ACTIONS})
source_group( Renderers FILES ${PLUGIN_RENDERERS})
source_group( Graphics FILES ${PLUGIN_GRAPHICS})
source_group( Resources FILES ${RESOURCES})

# -----------------------------------------------------------------------------
# CMake Target
# -----------------------------------------------------------------------------
# Create dynamic library for the plugin
add_library(${PROJECT_NAME} SHARED ${SOURCES} ${RESOURCES} ${RESOURCE_FILES} ${JSON})

if(USE_FAISS)
  message(STATUS "Compiling with -DUSE_FAISS")
  target_compile_definitions(
    ${PROJECT_NAME}
    PRIVATE
      USE_FAISS
  )
endif()


# -----------------------------------------------------------------------------
# Target Include Directories
# -----------------------------------------------------------------------------
# Include ManiVault headers, including system data plugins
target_include_directories(${PROJECT_NAME} PRIVATE "${ManiVault_INCLUDE_DIR}")

# -----------------------------------------------------------------------------
# Target Properties
# -----------------------------------------------------------------------------
# Request C++17
target_compile_features(${PROJECT_NAME} PRIVATE cxx_std_20)

# Enable unity build
if(MV_UNITY_BUILD)
    set_target_properties(${PROJECT_NAME} PROPERTIES UNITY_BUILD ON)
endif()

# -----------------------------------------------------------------------------
# Target Library Linking
# -----------------------------------------------------------------------------
# Link to Qt libraries
target_link_libraries(${PROJECT_NAME} PRIVATE Qt6::Widgets)
target_link_libraries(${PROJECT_NAME} PRIVATE Qt6::WebEngineWidgets)
target_link_libraries(${PROJECT_NAME} PRIVATE Qt6::OpenGL)
target_link_libraries(${PROJECT_NAME} PRIVATE Qt6::OpenGLWidgets)

# Link to ManiVault and data plugins
target_link_libraries(${PROJECT_NAME} PRIVATE ManiVault::Core)
target_link_libraries(${PROJECT_NAME} PRIVATE ManiVault::PointData)
target_link_libraries(${PROJECT_NAME} PRIVATE ManiVault::ImageData)
target_link_libraries(${PROJECT_NAME} PRIVATE VolumeDataPlugin)

# --- Link OpenMP ---
if(OpenMP_CXX_FOUND)
    target_link_libraries(${PROJECT_NAME} PRIVATE OpenMP::OpenMP_CXX)
endif()

# Link the precompiled Faiss library from Conda.
if(USE_FAISS)
    target_link_libraries(${PROJECT_NAME} PRIVATE faiss)
endif()

# If you have a precompiled hnswlib library, link it here.
# For header-only integration, this may not be necessary.
# target_link_libraries(${PROJECT_NAME} PRIVATE hnswlib)

# -----------------------------------------------------------------------------
# Target Installation
# -----------------------------------------------------------------------------
# Install the shared plugin library to the "Plugins" folder in the ManiVault install directory
install(TARGETS ${PROJECT_NAME}
    RUNTIME DESTINATION Plugins COMPONENT PLUGINS # Windows .dll
    LIBRARY DESTINATION Plugins COMPONENT PLUGINS # Linux/Mac .so
)

add_custom_command(TARGET ${PROJECT_NAME} POST_BUILD
    COMMAND "${CMAKE_COMMAND}"
        --install ${CMAKE_CURRENT_BINARY_DIR}
        --config $<CONFIGURATION>
        --prefix ${ManiVault_INSTALL_DIR}/$<CONFIGURATION>
)

set_target_properties(${PROJECT_NAME}
    PROPERTIES
    FOLDER ViewPlugins
)

# -----------------------------------------------------------------------------
# Miscellaneous
# -----------------------------------------------------------------------------
# Automatically set the debug environment (command + working directory) for MSVC
if(MSVC)
    set_property(TARGET ${PROJECT_NAME} PROPERTY VS_DEBUGGER_WORKING_DIRECTORY $<IF:$<CONFIG:DEBUG>,${ManiVault_INSTALL_DIR}/debug,${ManiVault_INSTALL_DIR}/release>)
    set_property(TARGET ${PROJECT_NAME} PROPERTY VS_DEBUGGER_COMMAND $<IF:$<CONFIG:DEBUG>,${ManiVault_INSTALL_DIR}/debug/ManiVault\ Studio.exe,${ManiVault_INSTALL_DIR}/release/ManiVault\ Studio.exe>)
endif()